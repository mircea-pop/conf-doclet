def gitMavenRepo = 'https://github.com/mircea-pop/maven-repo.git'

def localDeploymentFolder = "${buildDir}/maven-repo"

def isDevBuild
def isReleaseBuild
def isCi

def uploadRepositoryUrl

if (hasProperty("release")) {
    isReleaseBuild = true
    uploadRepositoryUrl = "file:${localDeploymentFolder}/releases"
}else if (hasProperty("ci")) {
    isCi = true
    project.version += '-SNAPSHOT'
    uploadRepositoryUrl = "file:${localDeploymentFolder}/snapshots"
}
else {
    isDevBuild = true
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }else {
            mavenDeployer() {
                name = "GitHub"

                repository(url: uploadRepositoryUrl) {
                }

                pom.project  {
                    name project.name
                    packaging 'jar'
                    description project.description
                    url 'https://github.com/mircea-pop/conf-doclet.git'

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    scm {
                        url 'scm:git@github.com:mircea-pop/conf-doclet.git'
                        connection 'scm:git@github.com:mircea-pop/conf-doclet.git'
                        developerConnection 'scm:git@github.com:mircea-pop/conf-doclet.git'
                    }

                    developers {
                        developer {
                            id 'mircea-pop'
                            name 'Mircea Pop'
                        }
                    }
                }
            }
        }
    }
}

apply plugin: 'github-pages'

githubPages {
    repoUri = gitMavenRepo
    workingPath = localDeploymentFolder
}

task publish2Git(dependsOn: pushGhPages){ description = "Runs the maven installer and pushes the artifacts to the Git-based maven repository"  }

task commit2Git(dependsOn: commitGhPages){ description = "Runs the maven installer and commits the artifacts to the Git-based maven repository"    }

addGhPages.dependsOn uploadArchives
